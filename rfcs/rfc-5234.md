# RFC 5234

Augmented BNF for Syntax Specifications: ABNF.

A metalanguage; a formal system of a language.

## Rule Names

Rule names are case insensitive., typically denoted `<rulename>`.

## General Rule Definition Form

Form:

	name = elements crlf

- `name`: name of rule
- `elements`: one or more rule names or terminal specifications (see next sections)
- `crlf`: end-of-line indicator (carriage return plus line feed, `\r\n`)

## Terminal Values (Characters)

Rules resolve into a string of terminal values, sometimes called characters.

Numeric characters may be specified as the percent sign `%`, followed by the base (`b` = binary, `d` = decimal, and `x` = hexadecimal), followed by the value, or concatenation of values (indicated by `.`).

Characters may be written:

- as string literals ("command string"); these are case-insensitive and may only be US-ASCII
- in decimal, i.e. `%d13` denotes \r or decimal 13, `chr(13)`
- in hex, i.e. `%x0D` denotes \r or hex 0d, `chr(0x0d)`
- in binary

A concatenated string of such values is specified compactly, using a period (".") to indicate a separation of characters within that value:

```
%d13.10      ; \r (decimal 13) plus \n (decimal 10)
```

## Operators

### Comment

**Syntax**: `; Comment`

**Meaning**: A semicolon starts a comment that continues to the end of line

**Example**:

```
CR             =  %x0D    ; carriage return
                          ; this is also a comment
```

### Concatenation

**Syntax**: `Rule1 Rule2`

**Meaning**: Effectively joins multiple rules into one

**Example**:

```
foo         =  %x61           ; a, hex 61 / decimal 97
bar         =  %x62           ; b, hex 62 / decimal 98
mumble      =  foo bar foo    ; matches "aba"
```

### Alternatives

**Syntax**: `Rule1 / Rule2`

**Meaning**: Either `Rule1` or `Rule2`

**Example**:

```
foo / bar
```

### Incremental Alternatives

**Syntax**: `Rule1 =/ Rule2`

**Meaning**: Allows for specifying alternatives in fragments, defining same rule in multiple lines

**Example**:

```
; same as ruleset     =  alt1 / alt2 / alt3 / alt4 / alt5

ruleset     =  alt1 / alt2
ruleset     =/ alt3
ruleset     =/ alt4 / alt5
```

### Value Range

**Syntax**: `%c##-##`

**Meaning**: Dash (`-`) indicates range of accepted values (lexicographically)

**Example**:

```
DIGIT       =  %x30-39  ; list(map(chr, range(0x30, 0x3a)))
```

### Sequence Group

**Syntax**: `(Rule1 Rule2)`

**Meaning**: Bind `Rule1 Rule2` as a single ordered element

**Example**:

```
elem (foo / bar) blat      ; matches (elem foo blat) or (elem bar blat)
elem foo / bar blat        ; matches (elem foo) or (bar blat)
(elem foo) / (bar blat)    ; matches (elem foo) or (bar blat)
```

### Variable Repetition

**Syntax**: `<a>*<b>element`

**Meaning**: At least `a` and at most `b` occurrences

Both `<a>` and `<b>` are optional integer values (defaults are 0 and infinity)

**Example**:

```
*<element>         ; 0 or more
1*<element>        ; requires at least one
3*3<element>       ; allows exactly 3 (see also nRule below)
1*2<element>       ; allows one or two
```

### Specific Repetition

**Syntax**: `<n>element`

**Meaning**: Exactly `<n>` occurrences of `<element>`.

Equivalent to `<n>*<n>element`

**Example**:

```
2DIGIT       ; 2-digit number
3ALPHA       ; string of three alphabetic characters
```

### Optional Sequence

**Syntax**: `[RULE]`

**Meaning**: 0 or 1 occurrences

**Example**:

```
[foo bar]    ; equivalent to *1(foo bar)
```

## Operator Precedence

```
1. Rule name, prose-val, Terminal value    ; Highest (binds tighest)
2. Comment
3. Value range
4. Repetition
5. Grouping, Optional
6. Concatenation
7. Alternative         						; Lowest (binds most loosely)
```

## Core Rules

Presented in order of "build-up":

```
ALPHA   =  %x41-5A / %x61-7A   ; A-Z / a-z, [chr(i) for i in range(0x41, 0x5b)]
CHAR    =  %x01-7F  		; any 7-bit US-ASCII character, excluding NUL (0x00)

BIT     =  "0" / "1"
DIGIT   =  %x30-39  		; 0 thru 9, decimal 48-5
OCTET   =  %x00-FF  ; 8 bits of data, 0 thru 255 decimal
HEXDIG  =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

CR      =  %x0D				; carriage return, chr(0x0d), \r
LF      =  %x0A 			; line feed/newline (\n)
CRLF    =  CR LF 			; carriage return + line feed

DQUOTE  =  %x22   			; double quote, "

SP      =  %x20     	; space, ' '
HTAB    =  %x09   		; horizontal tab, \t
WSP     =  SP / HTAB  	; white space, space or horizontal tab

CTL     =  %x00-1F / %x7F   ; control characters, decimal 1-31 or 127
VCHAR   =  %x21-7E  ; visible (printing) characters
```

## ABNF Definition of ABNF

Unsurprisingly, ABNF defines its elements/lexicon in ABNF, found in [section 4](https://tools.ietf.org/html/rfc5234#section-4) of the RFC.

Here are a few examples:

```
; starts with letter, followed by 0 or more letters, digits, or hyphen
rulename       =  ALPHA *(ALPHA / DIGIT / "-")

c-nl           =  comment / CRLF   ; comment or newline

; literal "b" followed by at least one BIT
; or
; literal "b" followed by a range
bin-val        =  "b" 1*BIT [ 1*("." 1*BIT) / ("-" 1*BIT) ]

; literal "%" followed by one of bin-, dec-, or hex- val
num-val        =  "%" (bin-val / dec-val / hex-val)
```

## Example: U.S. Postal Address

From [Wikipedia](https://en.wikipedia.org/wiki/Augmented_Backus%E2%80%93Naur_form):

```
postal-address   = name-part street zip-part

name-part        = *(personal-part SP) last-name [SP suffix] CRLF
name-part        =/ personal-part CRLF

personal-part    = first-name / (initial ".")
first-name       = *ALPHA
initial          = ALPHA
last-name        = *ALPHA
suffix           = ("Jr." / "Sr." / 1*("I" / "V" / "X"))

street           = [apt SP] house-num SP street-name CRLF
apt              = 1*4DIGIT
house-num        = 1*8(DIGIT / ALPHA)
street-name      = 1*VCHAR

zip-part         = town-name "," SP state 1*2SP zip-code CRLF
town-name        = 1*(ALPHA / SP)
state            = 2ALPHA
zip-code         = 5DIGIT ["-" 4DIGIT]
```
